plugins {
    id "io.qameta.allure" version "2.8.1"  //  https://github.com/allure-framework/allure-gradle/releases
    id "java"
}

group getGroup()
version getVersion()

sourceCompatibility = JavaVersion.current()
targetCompatibility = JavaVersion.current()

compileJava {
    enabled = false
    options.fork = true
    options.incremental = true
}

processResources {
    enabled = false
}

compileTestJava {
    enabled = true
    options.fork = true
    options.incremental = true
}

processTestResources {
    enabled = true
}

clean {
    delete rootProject.buildDir
}

test {
    debug = false
    forkEvery = 100
    maxParallelForks = Runtime.runtime.availableProcessors().intdiv(2) ?: 1
    ignoreFailures(true)
    useJUnitPlatform()
    reports {
        html.enabled = false
        junitXml.enabled = false
    }
    testLogging {
        showStandardStreams = false
        exceptionFormat = "full"
    }
}

allure {
    version = "2.13.5"  //  https://github.com/allure-framework/allure2/releases
    autoconfigure = true
    configuration = "testImplementation"
    clean = true
    allureJavaVersion = "latest.release"
    aspectjweaver = true
    aspectjVersion = "latest.release"
    resultsDir = file("allure-results")
    reportDir = file("allure-report")
    useJUnit5 {
        version = "latest.release"
    }
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation("org.junit.jupiter:junit-jupiter-api:latest.release")
    testImplementation("com.codeborne:selenide:latest.release")
    testImplementation("io.qameta.allure:allure-selenide:latest.release")
    testImplementation("com.github.automatedowl:allure-environment-writer:latest.release")
    testRuntimeOnly("org.slf4j:slf4j-nop:latest.release")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:latest.release")
}
